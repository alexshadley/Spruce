file = _{ SOI ~ (top_stmt | empty_line)* ~ EOI }

top_stmt = _{ ( interop_decl | function_decl | type_decl | assign ) ~ "\n" }
stmt = _{ ( assign | fn_call | case ) ~ "\n" }

type_decl = { "type" ~ up_id ~ type_params ~ "{\n" ~ (type_option ~ "\n")+ ~ "}" }
type_params = { ("(" ~ lo_id ~ ("," ~ lo_id)* ~ ")")? }
type_option = { up_id ~ ("(" ~ type_id ~ ("," ~ type_id)* ~ ")")? }

type_id = { up_id ~ ("(" ~ type_id ~ ("," ~ type_id)* ~ ")")? | lo_id }

interop_decl = { "interop" ~ lo_id ~ fn_args }

function_decl = { lo_id ~ fn_args ~ "{\n" ~ body ~ "}" }
fn_args = { "(" ~ (fn_arg ~ ("," ~ fn_arg)*)? ~ ")" ~ ("->" ~ type_ann)? }
fn_arg = { lo_id ~ (":" ~ type_ann)? }

assign = { target ~ "=" ~ valued }

target = _{ mutable_tgt | update_tgt | lo_id }
mutable_tgt = { "mut" ~ lo_id }
update_tgt = { lo_id ~ ":" }

// type annotation definitions
type_ann = _{ func | unit | adt | tvar }
    unit = { "()" }
    adt = { up_id ~ ("(" ~ type_ann ~ ("," ~ type_ann)* ~ ")")? }
    tvar = { lo_id }
    func = { "(" ~ (type_ann ~ ("," ~ type_ann)*)? ~ ")" ~ "->" ~ type_ann }


// something that can be reduced to a value
valued = _{ case | expr }

case = { "case" ~ expr ~ "{\n" ~ case_option+ ~ "}" }
case_option = { case_pattern ~ "->" ~ (expr | "{\n" ~ body ~ "}") ~ "\n" }
case_pattern = { up_id ~ ( "(" ~ lo_id ~ ( "," ~ lo_id )*  ~ ")")? }

body = { (stmt | empty_line)* ~ (valued ~ "\n")? }

expr = { term ~ (operation ~ term)* }
term = _{ fn_call | val_call | id | num | "(" ~ expr ~ ")" }

fn_call = { lo_id ~ "(" ~ (expr ~ ("," ~ expr)* )? ~ ")" }
val_call = { up_id ~ "(" ~ (expr ~ ("," ~ expr)* )? ~ ")" }

id = _{ up_id | lo_id }
up_id = @{ ASCII_ALPHA_UPPER ~ ASCII_ALPHANUMERIC* }
lo_id = @{ ASCII_ALPHA_LOWER ~ ASCII_ALPHANUMERIC* }

empty_line = _{ "\n" }

num = @{ int ~ ("." ~ ASCII_DIGIT*)? ~ (^"e" ~ int)? }
    int = { ("+" | "-")? ~ ASCII_DIGIT+ }

operation = _{ add | subtract | multiply | divide | power | modulus | eq | not_eq | lt_eq | gt_eq | lt | gt }
    add      = { "+" }
    subtract = { "-" }
    multiply = { "*" }
    divide   = { "/" }
    power    = { "^" }
    modulus  = { "%" }
    eq       = { "==" }
    not_eq   = { "!=" }
    lt_eq    = { "<=" }
    gt_eq    = { ">=" }
    lt       = { "<" }
    gt       = { ">" }

WHITESPACE = _{ " " | "\t" }
