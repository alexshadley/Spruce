file = _{ SOI ~ (top_stmt | empty_line)* ~ EOI }

top_stmt = _{ ( function_decl | type_decl | assign ) ~ "\n" }
stmt = _{ ( assign | fn_call | case ) ~ "\n" }

type_decl = { "type" ~ id ~ type_params ~ "{\n" ~ (type_option ~ "\n")+ ~ "}" }
type_params = { ("(" ~ id ~ ("," ~ id)* ~ ")")? }
type_option = { id ~ ("(" ~ type_id ~ ("," ~ type_id)* ~ ")")? }

type_id = { id ~ ("(" ~ type_id ~ ("," ~ type_id)* ~ ")")? }

function_decl = { id ~ fn_args ~ "{\n" ~ body ~ "}" }
fn_args = { "(" ~ (id ~ ("," ~ id)* )? ~ ")" }

assign = { target ~ "=" ~ valued }

target = _{ mutable_tgt | update_tgt | id }
mutable_tgt = { "mut" ~ id }
update_tgt = { id ~ ":" }

// something that can be reduced to a value
valued = _{ case | expr }

case = { "case" ~ expr ~ "{\n" ~ case_option+ ~ "}" }
case_option = { case_pattern ~ "->" ~ (expr | "{\n" ~ body ~ "}") ~ "\n" }
case_pattern = { id ~ ( "(" ~ id ~ ( "," ~ id )*  ~ ")")? }

body = { (stmt | empty_line)* ~ (valued ~ "\n")? }

expr = { term ~ (operation ~ term)* }
term = _{ fn_call | id | num | string | "(" ~ expr ~ ")" }

fn_call = { id ~ "(" ~ (expr ~ ("," ~ expr)* )? ~ ")" }

id = @{ ASCII_ALPHA ~ ASCII_ALPHANUMERIC* }

empty_line = _{ "\n" }

num = @{ int ~ ("." ~ ASCII_DIGIT*)? ~ (^"e" ~ int)? }
    int = { ("+" | "-")? ~ ASCII_DIGIT+ }

hex = _{ '0'..'9' | 'a'..'f' | 'A'..'F' }
unicode_hex = { hex{1, 6} }

predefined = { "n" | "r" | "t" | "\\" | "0" | "\"" | "'" }
byte       = { "x" ~ hex{2} }
unicode    = { "u" ~ "{" ~ unicode_hex ~ "}" }
escape     = { "\\" ~ (predefined | byte | unicode) }

raw_string = { (!("\\" | "\"") ~ ANY)+ }
string = { "\"" ~ (raw_string | escape)* ~ "\""}

operation = _{ concat | add | subtract | multiply | divide | power | modulus | eq | not_eq | lt_eq | gt_eq | lt | gt }
    concat   = { "++" }
    add      = { "+" }
    subtract = { "-" }
    multiply = { "*" }
    divide   = { "/" }
    power    = { "^" }
    modulus  = { "%" }
    eq       = { "==" }
    not_eq   = { "!=" }
    lt_eq    = { "<=" }
    gt_eq    = { ">=" }
    lt       = { "<" }
    gt       = { ">" }

WHITESPACE = _{ " " | "\t" }
